plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '1.2.4'
  id 'net.saliman.cobertura' version '2.3.1'
  id 'com.github.kt3k.coveralls' version '2.6.3'
  id "nebula.deb" version "4.1.0"
}

version = '1.2.7'
group = 'lt.nkts'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  jcenter()
}

dependencies {
  compile 'org.apache.cassandra:cassandra-all:3.7'
  compile 'com.ecwid.consul:consul-api:1.1.9'
  compile 'com.google.guava:guava:19.0'
}

shadowJar {
  mergeServiceFiles()
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
}

task packageDeb(type: Deb) {
  packageName = 'cassandra-consul-seed-provider'
  requires('boundary-platform-java')
  into '/opt/cassandra-consul-seed-provider'
  from(shadowJar.outputs.files) {
    into 'lib'
  }
}

configurations {
  runtime.exclude module: 'logback-core'
  runtime.exclude module: 'logback-classic'
  runtime.exclude module: 'slf4j-api'
  runtime.exclude module: 'log4j-over-slf4j'
  runtime.exclude module: 'jcl-over-slf4j'
  runtime.exclude module: 'cassandra-all'
}

cobertura.coverageFormats = ['html', 'xml']
